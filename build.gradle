import java.text.SimpleDateFormat

plugins {
    id 'checkstyle'
    id "com.github.spotbugs" version "5.0.+"
    id 'com.github.johnrengelman.shadow' version '7.1.+'
    id 'java'
    id 'maven-publish'
}

group = "com.crimsonwarpedcraft.cwcommons"

static def getTime() {
    SimpleDateFormat sdf = new SimpleDateFormat("yyMMdd-HHmm")
    sdf.setTimeZone(TimeZone.getTimeZone("UTC"))
    return sdf.format(new Date()).toString()
}

// Load version from string
String shortVersion = null
if (hasProperty('ver')) {
    if (ver.charAt(0) == "v") {
        shortVersion = ver.substring(1)
    } else {
        shortVersion = ver
    }
}

if (shortVersion == null || shortVersion == "") {
    shortVersion = getTime()
}

// Add snapshot identifier to version
version = shortVersion + "-SNAPSHOT"

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    maven {
        name 'papermc'
        url 'https://papermc.io/repo/repository/maven-public/'
        content {
            includeModule("io.papermc.paper", "paper-api")
            includeModule("io.papermc", "paperlib")
            includeModule("net.md-5", "bungeecord-chat")
        }
    }

    maven {
        name 'command-api'
        url "https://repo.codemc.org/repository/maven-public/"
        content {
            includeModule("dev.jorel", "commandapi-core")
            includeModule("dev.jorel", "commandapi-annotations")
        }
    }

    mavenCentral()
}

dependencies {
    annotationProcessor 'dev.jorel:commandapi-annotations:8.0.0'
    compileOnly 'dev.jorel:commandapi-annotations:8.0.0'
    compileOnly 'dev.jorel:commandapi-core:8.1.0'
    compileOnly 'io.papermc.paper:paper-api:1.18.1-R0.1-SNAPSHOT'
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.7.0'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'io.papermc:paperlib:1.0.7'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
    testCompileOnly 'com.github.spotbugs:spotbugs-annotations:4.7.0'
    testImplementation 'io.papermc.paper:paper-api:1.18.1-R0.1-SNAPSHOT'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

processResources {
    filesMatching("**/plugin.yml") {
        expand ( NAME: rootProject.name, VERSION: version, PACKAGE: project.group.toString() )
    }
}

checkstyle {
    toolVersion '10.2+'
    maxWarnings = 0
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
    }
}

spotbugsMain {
    reports {
        xml.required = false
        html.required = true
    }
}

spotbugsTest {
    reports {
        xml.required = false
        html.required = true
    }
}

shadowJar {
    archiveClassifier.set('')
    relocate 'io.papermc.lib', 'shadow.io.papermc.paperlib'
    relocate 'dev.jorel.commandapi-core', 'shadow.dev.jorel.commandapi-core'
    relocate 'dev.jorel.commandapi-annotations', 'shadow.dev.jorel.commandapi-annotations'
    minimize()
}

publishing {
    repositories {
        maven {
            name = "sps"
            url = uri("https://mvn.snowypeaksystems.com/repository/maven-snapshots/")
            credentials {
                username = System.getenv("SPS_MVN_USER")
                password = System.getenv("SPS_MVN_PASS")
            }
        }
    }
    publications {
        spsmvn(MavenPublication) {
            artifact shadowJar.archiveFile
        }
    }
}

// Disable jar and replace with shadowJar
jar.enabled = false
assemble.dependsOn(shadowJar)

// We need to build before we publish
publish.dependsOn(build)

task configureRelease {
    doLast {
        // Trim the "-SNAPSHOT" extension from version
        version = shortVersion
        // Change the publishing repository to release server
        publishing.repositories.getByName(
                "sps",
                {
                    url = uri("https://mvn.snowypeaksystems.com/repository/maven-releases/")
                }
        )
    }
}

// Make sure configuration runs in the correct order
build.mustRunAfter(configureRelease)

task release {
    dependsOn build
    dependsOn publish
    dependsOn configureRelease

    doLast {
        // Rename final JAR to trim off version information
        shadowJar.archiveFile.get().getAsFile()
                .renameTo(buildDir.toString() + File.separator + 'libs' + File.separator
                        + rootProject.name + '.jar')
    }
}
