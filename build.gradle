import java.text.SimpleDateFormat

plugins {
    id 'checkstyle'
    id "com.github.spotbugs" version "4.7.+"
    id 'com.github.johnrengelman.shadow' version '7.1.+'
    id 'java'
    id 'maven-publish'
}

group = "com.crimsonwarpedcraft.cwcommons"

static def getTime() {
    SimpleDateFormat sdf = new SimpleDateFormat("yyMMdd-HHmm")
    sdf.setTimeZone(TimeZone.getTimeZone("UTC"))
    return sdf.format(new Date()).toString()
}

// Load version from string
String shortVersion = null
if (hasProperty('ver')) {
    if (ver.charAt(0) == "v") {
        shortVersion = ver.substring(1)
    } else {
        shortVersion = ver
    }
}

if (shortVersion == null || shortVersion == "") {
    shortVersion = getTime()
}

// Add snapshot identifier to version
version = shortVersion + "-SNAPSHOT"

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    maven {
        url 'https://mvn.snowypeaksystems.com/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.6.0'
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'io.papermc:paperlib:1.0.7'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
    testCompileOnly 'com.github.spotbugs:spotbugs-annotations:4.5.2'
    testImplementation 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

processResources {
    filesMatching("**/plugin.yml") {
        expand ( NAME: rootProject.name, VERSION: version, PACKAGE: project.group.toString() )
    }
}

checkstyle {
    toolVersion '9.0+'
    maxWarnings = 0
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
    }
}

spotbugsMain {
    reports {
        xml.required = false
        html.required = true
    }
}

spotbugsTest {
    reports {
        xml.required = false
        html.required = true
    }
}

shadowJar {
    archiveClassifier.set('')
    relocate 'io.papermc.lib', 'shadow.io.papermc.paperlib'
    minimize()
}

publishing {
    repositories {
        maven {
            name = "sps"
            url = uri("https://mvn.snowypeaksystems.com/repository/maven-snapshots/")
            credentials {
                username = System.getenv("SPS_MVN_USER")
                password = System.getenv("SPS_MVN_PASS")
            }
        }
    }
    publications {
        spsmvn(MavenPublication) {
            artifact shadowJar.archiveFile
        }
    }
}

// Disable jar and replace with shadowJar
jar.enabled = false
assemble.dependsOn(shadowJar)

// We need to build before we publish
publish.dependsOn(build)

task configureRelease {
    doLast {
        // Trim the "-SNAPSHOT" extension from version
        version = shortVersion
        // Change the publishing repository to release server
        publishing.repositories.getByName(
                "sps",
                {
                    url = uri("https://mvn.snowypeaksystems.com/repository/maven-releases/")
                }
        )
    }
}

// Make sure configuration runs in the correct order
build.mustRunAfter(configureRelease)

task release {
    dependsOn build
    dependsOn publish
    dependsOn configureRelease

    doLast {
        // Rename final JAR to trim off version information
        shadowJar.archiveFile.get().getAsFile()
                .renameTo(buildDir.toString() + File.separator + 'libs' + File.separator
                        + rootProject.name + '.jar')
    }
}
